apply plugin: "java"

// get git version
def describeOutput = new ByteArrayOutputStream()
project.exec({
  commandLine(["git", "describe", "HEAD", "--tags"])
  standardOutput = describeOutput
})
def gitCommit = describeOutput.toString().replace("\n", "")
def suffixOutput = new ByteArrayOutputStream()
project.exec({
  commandLine(["bash", "-c", "git status --porcelain | head -n 1 | sed 's/..*/-dev/'"])
  standardOutput = suffixOutput
})
def modificationSuffix = suffixOutput.toString().trim().replace("\n", "")
def gitVersion = gitCommit + modificationSuffix

compileJava {
  sourceCompatibility = 1.7
  targetCompatibility = 1.7
}

String mainClass = "mapper.Main"

jar {
  manifest {
    attributes 'Main-Class': mainClass
  }
}

dependencies {
  test {
    testImplementation "junit:junit:4.13"
  }
}

def projectName = project.name
task release(type: Copy) {
  from "build/libs"
  include "${projectName}.jar"
  into "build/libs"
  rename("${projectName}.jar", "quick-variants-${gitVersion}.jar")
  dependsOn("build")
}

def expandProperties = new HashMap<String, String>()
expandProperties["mapperVersion"] = gitVersion
project.tasks["processResources"].configure { processTask ->
  processTask.expand(expandProperties)
  processTask.inputs.property("properties", expandProperties)
}

repositories {
  mavenCentral()
}

